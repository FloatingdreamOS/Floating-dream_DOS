;Floating dream-DOS
;Copyright © 2022 Floatingdream All Rights Reserved.
;floatingdream.vxz.vin
IVTSEGMENT			EQU		0
FILETYPESEGMENT		EQU		0BE0H	; 800H+3E0H=BE0H
CMOS_SENT_PORT		EQU		0x70
CMOS_READ_PORT		EQU		0x71
CMOS_WEEK_DAY		EQU		0x6
INITSYSCALL:
	CALL	REGINTHANDLER36
	RET

REGINTHANDLER36:	; 36号中断
	MOV	AX,IVTSEGMENT
	MOV	ES,AX
	MOV	WORD[ES:36H*4],INTHANDLER36	; 偏移地址
	MOV	AX,CS
	MOV	WORD[ES:36H*4+2],AX	; 段地址
	RET

INTHANDLER36:	; 36号中断程序
	CMP	AH,00H
	JE	.VERSIONCALL
	CMP	AH,01H
	JE	.PUTSTRCALL
	CMP	AH,02H
	JE	.NEWLINECALL
	CMP	AH,03H
	JE	.RETURNCALL
	CMP	AH,04H
	JE	.TIMECALL
	CMP	AH,05H
	JE	.DATECALL
	CMP	AH,06H
	JE	.FINDFILECALL
	CMP	AH,07H
	JE	.FINDFREEFILECALL
	CMP	AH,08H
	JE	.COMMANDCALL
	CMP	AH,09H
	JE	.DRIVECALL
	JMP	.END
.VERSIONCALL:
	MOV	AX,107CH	; 1.07C
	JMP	.END
.PUTSTRCALL:
	PUSH	AX
	PUSH	SI
.PUTSTRCALL.LOOP:
	MOV	AL,[DS:SI]
	CMP	AL,0	; 如果[SI]=0
	JE	.PUTSTRCALL.END	; 就结束
	MOV	AH,0EH
	INT	10H
	INC	SI
	JMP	.PUTSTRCALL.LOOP
.PUTSTRCALL.END:
	POP	SI
	POP	AX
	JMP	.END
.NEWLINECALL:
	PUSH	AX
	MOV	AH,0EH
	MOV	AL,0DH
	INT	10H
	MOV	AL,0AH
	INT	10H
	POP	AX
	JMP	.END
.RETURNCALL:
	JMP	SHORT	.RETURNCALL.CODESTART
	DW	0,SYSTEMADDRESS / 10H
.RETURNCALL.CODESTART:
	MOV	AX,SYSTEMADDRESS / 10H
	MOV	DS,AX
	MOV	SI,.RETURNCALL+2
	MOV	EDX,20201220H
	JMP	FAR	[SI]
.TIMECALL:
	PUSH	AX
	PUSH	CX
	PUSH	DX
	MOV	AH,02H
	INT	1AH
	MOV	BYTE[DS:SI],CH
	MOV	BYTE[DS:SI+1],CL
	MOV	BYTE[DS:SI+2],DH
	POP	DX
	POP	CX
	POP	AX
	JMP	.END
.DATECALL:
	PUSH	AX
	PUSH	CX
	PUSH	DX
	MOV	AH,04H
	INT	1AH
	MOV	BYTE[DS:SI],CH
	MOV	BYTE[DS:SI+1],CL
	MOV	BYTE[DS:SI+2],DH
	MOV	BYTE[DS:SI+3],DL
	POP	DX
	POP	CX
	POP	AX
	JMP	.END
.FINDFILECALL:
	PUSH	ES
	PUSH	AX
	PUSH	CX
	PUSH	DI
	PUSH	SI
	MOV	CX,12
	MOV	DI,0
	MOV	BX,0
	MOV	DX,0
.FINDFILECALL.LOOP:
	MOV	AL,[DS:SI]
	MOV	AH,[ES:DI]
	CMP	AH,AL
	JNE	.FINDFILECALL.NEXTFILE
	INC	SI
	INC	DI
	LOOP	.FINDFILECALL.LOOP
	MOV	BX,ES
	MOV	AX,ES
	ADD	AX,1H
	MOV	ES,AX
	MOV	CX,[ES:10]	; 获取簇信息
	MOV	AX,0
.FINDFILECALL.MUL:
	ADD	AX,20H
	LOOP	.FINDFILECALL.MUL
	ADD	AX,FILETYPESEGMENT
	MOV	DX,AX	; 计算完文件段地址 将其放入DX
.FINDFILECALL.END:
	POP	SI
	POP	DI
	POP	CX
	POP	AX
	POP	ES
	JMP	.END
.FINDFILECALL.NEXTFILE:
	MOV	AX,ES
	ADD	AX,2H
	MOV	ES,AX
	MOV	DI,0
	POP	SI	; 方便取出SI的原始值
	PUSH	SI	; 重新再存入
	MOV	CX,12
	CMP	BYTE[ES:DI],0
	JE	.FINDFILECALL.END
	JMP	.FINDFILECALL.LOOP
.FINDFREEFILECALL:
	PUSH	AX
	PUSH	ES
.FINDFREEFILECALL.LOOP:
	MOV	AL,[ES:0]
	CMP	AL,0
	JE	.FINDFREEFILECALL.OK
	CMP	AL,0xE5		; 文件被删除
	JE	.FINDFREEFILECALL.OK
	MOV	AX,ES
	ADD	AX,2H
	MOV	ES,AX
	JMP	.FINDFREEFILECALL.LOOP
.FINDFREEFILECALL.OK:
	MOV	BX,ES
	POP	ES
	POP	AX
	JMP	.END
.COMMANDCALL:
	PUSHA
	PUSH	DS
	PUSH	ES
	MOV	AX,SYSTEMADDRESS / 10H
	MOV	ES,AX
	MOV	DI,0
.COMMANDCALL.LOOP:
	MOV	AL,[DS:SI]
	CMP	AL,0
	JE	.COMMANDCALL.OK
	MOV	[ES:CMDLINE+DI],AL
	INC	DI
	INC	SI
	JMP	.COMMANDCALL.LOOP
.COMMANDCALL.OK:
	MOV	AX,SYSTEMADDRESS / 10H
	MOV	DS,AX
	CALL	COMMAND
	POP	ES
	POP	DS
	POPA
	JMP	.END
.DRIVECALL:
	PUSH	DS
	PUSH	DX
	MOV	DX,SYSTEMADDRESS / 10H
	MOV	DS,DX
	MOV	AL,[DRIVETEMP+1]
	POP	DX
	POP	DS
	JMP	.END
.END:
	IRET	; POP CS POP IP POPF

MEMCPY:
; 拷贝内存到某处
; 寄存器：IN:DS:SI/ES:DI/CX
	MOV	AL,[DS:SI]
	MOV	[ES:DI],AL
	INC	SI
	INC	DI
	LOOP	MEMCPY
.CPYEND:
	RET